apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

repositories {

}

android {
    def majorVersion = 3
    def minorVersion = 0

    compileSdkVersion 30
    testOptions.unitTests.includeAndroidResources true
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    defaultConfig {
        applicationId "se.barsk.park"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 7
        versionName "$majorVersion.$minorVersion"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "boolean", "releaseBuild", "false"
        buildConfigField "int", "initialParkedCars", "0"
        signingConfig signingConfigs.debug
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "releaseBuild", "true"
        }
        mock_6 {
            buildConfigField "int", "initialParkedCars", "6"
            signingConfig signingConfigs.debug
            debuggable true
        }
        mock_5 {
            buildConfigField "int", "initialParkedCars", "5"
            signingConfig signingConfigs.debug
            debuggable true
        }
        mock_4 {
            buildConfigField "int", "initialParkedCars", "4"
            signingConfig signingConfigs.debug
            debuggable true
        }
        mock_3 {
            buildConfigField "int", "initialParkedCars", "3"
            signingConfig signingConfigs.debug
            debuggable true
        }
        mock_2 {
            buildConfigField "int", "initialParkedCars", "2"
            signingConfig signingConfigs.debug
            debuggable true
        }
        mock_1 {
            buildConfigField "int", "initialParkedCars", "1"
            signingConfig signingConfigs.debug
            debuggable true
        }
        mock_0 {
            buildConfigField "int", "initialParkedCars", "0"
            signingConfig signingConfigs.debug
            debuggable true
        }
    }

    flavorDimensions "build"
    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
            dimension "build"
        }
        prod {
            dimension "build"
        }
    }

    // Remove mockRelease and prodMock_* as it's not needed.
    android.variantFilter { variant ->
        if(variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true)
        } else if (variant.buildType.name.startsWith("mock")
                && variant.getFlavors().get(0).name == "prod") {
            variant.setIgnore(true)
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
        }
    }
}

dependencies {
    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:28.3.0')
    // Declare the dependencies for the desired Firebase products without specifying versions
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-dynamic-links'
    implementation 'com.google.firebase:firebase-messaging'

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.android.gms:play-services-auth:19.2.0'
    implementation 'com.github.kittinunf.fuel:fuel-android:2.3.1'
    implementation 'com.github.kittinunf.fuel:fuel-json:2.3.1'
    implementation 'com.github.kittinunf.result:result:3.1.0'
    implementation 'de.psdev.licensesdialog:licensesdialog:2.2.0'
    implementation 'net.danlew:android.joda:2.10.9.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.json:json:20210307'
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "org.robolectric:robolectric:4.6.1"
    testImplementation "org.robolectric:shadows-supportv4:4.6.1"
    testImplementation 'org.amshove.kluent:kluent-android:1.68'
}
repositories {
    mavenCentral()
    maven {
        url "https://maven.google.com"
    }

}
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

